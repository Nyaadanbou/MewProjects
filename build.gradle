plugins {
    id 'java-library'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '7.0.0'
}

ext.pluginNameUpper = "MoeCore"
ext.pluginNameLower = ext.pluginNameUpper.toLowerCase()
ext.majorVersion = 5
ext.minorVersion = 7
ext.minecraftVersion = "1.17.1"
sourceCompatibility = JavaVersion.VERSION_16
targetCompatibility = JavaVersion.VERSION_16

ext.buildNumber = System.env.BUILD_NUMBER == null ? "x" : "$System.env.BUILD_NUMBER"
ext.mavenDirectory = System.env.MAVEN_DIR == null ? "$projectDir/repo" : "$System.env.MAVEN_DIR"
ext.jdDirectory = System.env.JAVADOCS_DIR == null ? null : "$System.env.JAVADOCS_DIR"

group = 'co.mcsky'
archivesBaseName = "${pluginNameUpper}-mc$minecraftVersion"
version = "$majorVersion.$minorVersion".toString()

repositories {
    mavenCentral()

    maven { name = 'spigot'; url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { name = 'paper'; url = 'https://papermc.io/repo/repository/maven-public/' }
    maven { name = 'language'; url = 'https://repo.minebench.de/' }
    maven { name = 'command'; url = 'https://repo.aikar.co/content/groups/aikar/' }
    maven { name = 'helper'; url = 'https://repo.lucko.me' }
    maven { name = 'jitpack'; url = 'https://jitpack.io' }
    maven { name = 'nyaacore'; url = 'https://ci.nyaacat.com/maven/' }
    maven { name = 'essentialsx'; url = 'https://repo.essentialsx.net/releases/' }
    maven { name = 'sonatype-oss-snapshots'; url = 'https://oss.sonatype.org/content/repositories/snapshots/' }
}

dependencies {
    api 'org.spongepowered:configurate-core:4.1.2'
    api 'org.spongepowered:configurate-yaml:4.1.2'
    api 'org.spongepowered:configurate-gson:4.1.2'
    api 'org.spongepowered:configurate-hocon:4.1.2'
    api 'de.themoep.utils:lang-bukkit:1.3-SNAPSHOT'
    api 'co.aikar:acf-paper:0.5.0-SNAPSHOT'
    api 'com.mojang:authlib:1.5.25'
    api 'net.kyori:adventure-text-minimessage:4.1.0-SNAPSHOT'

    compileOnly 'io.papermc.paper:paper-api:1.17.1-R0.1-SNAPSHOT'
    compileOnly 'me.lucko:helper:5.6.8'
    compileOnly 'me.lucko:helper-sql:1.3.2'
    compileOnly 'me.lucko:helper-profiles:1.2.0'
    compileOnly 'net.luckperms:api:5.3'
    compileOnly('net.milkbowl.vault:VaultAPI:1.7') { transitive = false }
    compileOnly('net.essentialsx:EssentialsX:2.19.0') { transitive = false }

    // towny (for linking to towny server account)
    compileOnly 'com.github.TownyAdvanced:Towny:0.97.1.0'

    // nyaacore (for implementing system balance)
    compileOnly 'cat.nyaa:nyaacore:8.1-SNAPSHOT'
}

shadowJar {
    archiveClassifier.set('shadowed')

//    relocate 'org.spongepowered', 'co.mcsky.internal.org.spongepowered'
//    relocate 'de.themoep.utils', 'co.mcsky.internal.de.themoep.utils'
//    relocate 'com.mojang', 'co.mcsky.internal.com.mojang'
//    relocate 'co.aikar', 'co.mcsky.internal.co.aikar'
}

// compile options
compileJava {
    options.encoding = 'UTF-8'
}

// javadoc jar
task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set("javadoc")
    from javadoc.destinationDir
}

// source file jar
task sourcesJar(type: Jar) {
    archiveClassifier.set("sources")
    from sourceSets.main.allSource
}

// source file modification (modify version string)
processResources {
    filesMatching("**/plugin.yml") {
        expand 'version': project.version
    }
}

// maven publish
publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact sourcesJar
            artifact javadocJar
            groupId "co.mcsky"
            artifactId "MoeCore"
            version "$majorVersion.$minorVersion"
            from components.java
        }
    }
    repositories {
        maven {
            url "$mavenDirectory"
        }
    }
}

// javadoc generation options
javadoc {
    // javadoc output folder
    if (project.jdDirectory != null) destinationDir = file("${jdDirectory}/${pluginNameLower}-${version}")

    (options as StandardJavadocDocletOptions).with {
        links 'https://docs.oracle.com/en/java/javase/16/docs/api/'
        links 'https://hub.spigotmc.org/javadocs/spigot/'
        links 'https://google.github.io/guava/releases/21.0/api/docs/'
        links 'https://ci.md-5.net/job/BungeeCord/ws/chat/target/apidocs/'

        locale 'en_US'
        encoding 'UTF-8'
        docEncoding 'UTF-8'
        addBooleanOption('keywords', true)
        addStringOption('Xdoclint:none', '-quiet')

        addBooleanOption('html5', true)

        windowTitle = "${pluginNameUpper} Javadoc"
        docTitle = "${pluginNameUpper} (mc$minecraftVersion-${project.version})"
    }
}
