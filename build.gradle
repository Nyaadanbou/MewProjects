plugins {
    id 'java-library'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

java {
    toolchain.languageVersion.set(JavaLanguageVersion.of(17))
}

ext.pluginNameUpper = "MewCore"
ext.pluginNameLower = ext.pluginNameUpper.toLowerCase()
ext.majorVersion = 5
ext.minorVersion = 9
ext.minecraftVersion = "1.19"

ext.buildNumber = System.env.BUILD_NUMBER == null ? "x" : "$System.env.BUILD_NUMBER"
ext.mavenDirectory = System.env.MAVEN_DIR == null ? "$projectDir/repo" : "$System.env.MAVEN_DIR"
ext.jdDirectory = System.env.JAVADOCS_DIR == null ? null : "$System.env.JAVADOCS_DIR"

group = 'co.mcsky'
archivesBaseName = "${pluginNameUpper}-mc$minecraftVersion"
version = "$majorVersion.$minorVersion".toString()

repositories {
    mavenCentral()

    maven { name = 'spigot'; url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { name = 'paper'; url = 'https://papermc.io/repo/repository/maven-public/' }
    maven { name = 'language'; url = 'https://repo.minebench.de/' }
    maven { name = 'helper'; url = 'https://repo.lucko.me' }
    maven { name = 'jitpack'; url = 'https://jitpack.io' }
    maven { name = 'essentialsx'; url = 'https://repo.essentialsx.net/releases/' }
    maven { name = 'sonatype-oss-snapshots'; url = 'https://oss.sonatype.org/content/repositories/snapshots/' }
}

dependencies {
    // Non-plugin libraries, needs to be shadowed.
    // So projects that depends on these libraries
    // needs not to shadow these.
    api 'org.spongepowered:configurate-core:4.1.2'
    api 'org.spongepowered:configurate-yaml:4.1.2'
    api 'de.themoep.utils:lang-bukkit:1.3-SNAPSHOT'
    api 'com.mojang:authlib:1.5.25'

    // Note: kyori.adventure already comes with paper
    compileOnly 'io.papermc.paper:paper-api:1.19-R0.1-SNAPSHOT'

    // External plugins, should not be shadowed.
    compileOnly 'me.lucko:helper:5.6.10'
    compileOnly 'net.luckperms:api:5.4'
    compileOnly('net.milkbowl.vault:VaultAPI:1.7') { transitive = false }
    compileOnly('net.essentialsx:EssentialsX:2.19.0') { transitive = false }
    compileOnly 'com.github.TownyAdvanced:Towny:0.98.3.0' // for linking to towny server account
}

shadowJar {
    archiveClassifier.set('shadowed')
}

// compile options
compileJava {
    options.encoding = 'UTF-8'
    options.compilerArgs += ["-parameters"]
}

// javadoc jar
task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set("javadoc")
    from javadoc.destinationDir
}

// source file jar
task sourcesJar(type: Jar) {
    archiveClassifier.set("sources")
    from sourceSets.main.allSource
}

// source file modification (modify version string)
processResources {
    filesMatching("**/plugin.yml") {
        expand 'version': project.version
    }
}

// maven publish
publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact sourcesJar
            artifact javadocJar
            groupId "co.mcsky"
            artifactId "MewCore"
            version "$majorVersion.$minorVersion"
            from components.java
        }
    }
    repositories {
        maven {
            url "$mavenDirectory"
        }
    }
}

// javadoc generation options
javadoc {
    // javadoc output folder
    if (project.jdDirectory != null) destinationDir = file("${jdDirectory}/${pluginNameLower}-${version}")

    (options as StandardJavadocDocletOptions).with {
        links 'https://docs.oracle.com/en/java/javase/17/docs/api/'
        links 'https://hub.spigotmc.org/javadocs/spigot/'
        links 'https://google.github.io/guava/releases/21.0/api/docs/'
        links 'https://ci.md-5.net/job/BungeeCord/ws/chat/target/apidocs/'

        locale 'en_US'
        encoding 'UTF-8'
        docEncoding 'UTF-8'
        addBooleanOption('keywords', true)
        addStringOption('Xdoclint:none', '-quiet')

        addBooleanOption('html5', true)

        windowTitle = "${pluginNameUpper} Javadoc"
        docTitle = "${pluginNameUpper} (mc$minecraftVersion-${project.version})"
    }
}
